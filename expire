#!/usr/bin/perl -w
use strict;
use POSIX qw(strftime);

sub dirname($)
{
    my $filename = shift @_;
    $filename =~ m{(.*)/([^/]+)}  &&  return $1;
    return ".";
}

my $homedir = dirname($0);
chdir $homedir
    or die("Can't find home directory '$homedir'\n");

chdir 'data'
    or die("No data/ subdir!");

# delete really old files
foreach my $file (glob("*/*")) {
    if (-M $file > 14) {
	unlink($file);
    }
}

# create a warning if the newest file in a dir is too old
foreach my $dir (glob("*")) {
    my $newest = 1000;
    my $newest_stamp = 1000;
    
    my @files = glob("$dir/*");
    
    # the .stamp file notes that the dir has been updated, even if there are
    # new new interesting entries
    if (-e "$dir/.stamp") {
	$newest_stamp = -M "$dir/.stamp";
    } elsif (scalar(@files) == 0) {
        # maybe a brand new dir
        system("touch", "$dir/.stamp");
	$newest_stamp = $newest = 0;
    }	
    
    foreach my $file (glob("$dir/*")) {
	my $n = -M $file;
	$newest = $n if ($newest > $n);
	$newest_stamp = $n if ($newest_stamp > $n);
    }
    
    my $date = strftime("%Y%m%d-%H%M%S", localtime());
    my $efile = "$dir/$date." . int(rand(100000));
    
    if ($newest_stamp > 0.3 || $newest > 1.1) {
	open my $fh, ">$efile" or die("$efile: $!\n");
	my $host = `hostname`;
	$host =~ s/\s+//g;
	print $fh "WARNING (host=$host): No recent entries in this directory!\n";
	print $fh "\n";
	printf $fh "Latest stamp is %.2f days old.\n", $newest_stamp;
	printf $fh "Latest file is %.2f days old.\n", $newest;
    }
}
